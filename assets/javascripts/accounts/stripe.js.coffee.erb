
App.PurchaseFormController = Ember.Controller.extend
  actions: 
    close: -> 
      this.send "closeModal"

  key : '<%= settings.stripe_publishable_key %>'

  processingPurchase: false
  
  number: null
  cvc: null
  exp: null
  expMonth: (->
    if @get("exp")
      return Ember.$.payment.cardExpiryVal(@get("exp")).month || "MM"

    "MM"
  ).property("exp")
  expYear: (->
    if @get("exp")
      return Ember.$.payment.cardExpiryVal(@get("exp")).year || "YYYY"
    "YYYY"
  ).property("exp")

  cardType: (->
    Ember.$.payment.cardType(@get('number'))
  ).property('number')

  coupon: null,
  
  price: (->
    @get("model.amount")
  ).property("plan.amount")
  
  purchase:  ->
    @set('processingPurchase', true)
    Stripe.setPublishableKey(@get('key'))
    
    Stripe.card.createToken({
      number: @get('number')
      cvc: @get('cvc')
      exp_month: @get('expMonth')
      exp_year: @get('expYear')
    }, @didCreateToken.bind(this))

  didCreateToken: (status, response) ->
    if response.error
      @set('processingPurchase', false)
      @set('errors', response.error.message)
    else
      @postCharge(response)
  
  postCharge: (token) ->
    this.ajax("/settings/charge/" + @get("model.org.login"), {
      email:@get("model.org.billing_email"),
      card: token,
      plan: @get("model.plan")
    }).then(@didPurchase.bind(this), @purchaseDidError.bind(this))

  didPurchase: (response) ->
    @set('processingPurchase', false)
    @set("model.plan.purchased", true)
    this.send("close")
    
  purchaseDidError: (error) ->
    @set('errors', error.responseJSON.error.message)
    @set('processingPurchase', false)
       
  ajax: (url, data) ->
    controller = this

    new Ember.RSVP.Promise (resolve, reject) ->
      hash = {}
      hash.url = url
      hash.type = 'POST'
      hash.context = controller
      hash.data = data
      
      hash.success = (json) ->
        resolve(json)

      hash.error = (jqXHR, textStatus, errorThrown) ->
        reject(jqXHR)

      Ember.$.ajax(hash)

  
App.PurchaseFormView = App.ModalView.extend 
  processingPurchase: Ember.computed.alias('controller.processingPurchase')

App.CancelFormController = Ember.Controller.extend
  processingAction: false
  actions: 
    close: -> 
      this.send "closeModal"
    cancel:  ->
      @set('processingAction', true)
      this.ajax("/settings/profile/" + @get("model.org.login") + "/plans/" + @get('model.plan.id'),{})
        .then(@didCancel.bind(this), @cancelDidError.bind(this))
  didCancel: ->
    @set('processingAction', false)
    this.send "closeModal"
  cancelDidError: (error) ->
    @set('errors', error.responseJSON.error.message)
    @set('processingAction', false)


  ajax: (url, data) ->
    controller = this

    new Ember.RSVP.Promise (resolve, reject) ->
      hash = {}
      hash.url = url
      hash.type = 'DELETE'
      hash.context = controller
      hash.data = data
      
      hash.success = (json) ->
        resolve(json)

      hash.error = (jqXHR, textStatus, errorThrown) ->
        reject(jqXHR)

      Ember.$.ajax(hash)

App.CancelFormView = App.ModalView.extend 
  processingAction: Ember.computed.alias('controller.processingAction')


  

Ember.TextSupport.reopen
 attributeBindings: ["data-stripe", "autocomplete", "autocompletetype", "required"]

App.CvcField = Ember.TextField.extend
  required: true
  #pattern: "\d*"
  autocompletetype: "cc-csc"
  format: "123"
  placeholder: Ember.computed.alias("format")
  autocomplete: "off"
  didInsertElement: ->
    @$().payment("formatCardCVC")

App.CardNumberField = Ember.TextField.extend
  required: true
  #pattern: "\d*"
  autocompletetype: "cc-number"
  format: "1234 5678 9012 3456"
  placeholder: Ember.computed.alias("format")
  didInsertElement: ->
    @$().payment("formatCardNumber")

App.CardExpiryField = Ember.TextField.extend
  required: true
  autocompletetype: "cc-exp"
  format: "MM / YY"
  placeholder: Ember.computed.alias("format")
  didInsertElement: ->
    @$().payment("formatCardExpiry")

